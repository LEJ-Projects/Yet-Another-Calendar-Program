10 REM Yet Another Calendar Program
20 REM Lloyd Johnson
30 REM 23 August 2021
40 REM
50 DIM N(9,6),M(11,41)
60 CLS:PRINT"Yet Another Calendar Program (YACalP)"
70 PRINT"By Lloyd Johnson"
80 '
90 'Initialization
100 FORI=0TO9:FORJ=0 TO6:READN(I,J):NEXTJ:NEXTI
110 IF PEEK(1)=148THENDF$="COM:5I71XS"ELSEDF$="COM:57I1E"
120 '
130 'Get Year
140 INPUT"Which year (1900-2099)";YR
150 '
160 'Find Day of Week for January 1
170 Y=YR-100*INT(YR/100):YL=100*INT(YR/100)+1
180 YC=INT((YR-YL)/4):IFYR>1999THENYC=YC+1
190 IF YR<2000 THEN D0=1 ELSE D0=-1
200 IF YC<0THENYC=0
210 DT=Y+YC+D0
220 ID=DT-7*INT(DT/7)
230 '
240 'Get Output Name
250 INPUT"What is output name";OF$
260 IFOF$=""THENOF$=DF$
270 OPENOF$FOROUTPUTAS#1
280 '
290 'Build Calendar
300 DW=ID
310 FOR I=0TO11:MI=I+1:DI=1
320 IF MI<>2THEN350
330 IFINT(Y/4)=Y/4THENMX=29ELSEMX=28
340 GOTO360
350 IFMI=9ORMI=4ORMI=6ORMI=11THENMX=30ELSEMX=31
360 DI=1:FORJ=DWTO41
370 M(I,J)=DI:DI=DI+1
380 DW=DW+1
390 IF DW>6THENDW=0
400 IF DI>MXTHENJ=41
410 NEXT J
420 NEXT I
430 '
440 'Output the Calendar
450 GOSUB 490:'Year
460 GOSUB 770:'Calendar
470 END
480 '
490 'Print Year
500 PRINT#1,"   "STRING$(75,"*")
510 PRINT#1,"   "STRING$(75,"*")
520 FOR I=0TO6
530 PRINT#1,SPACE$(29);
540 D1=INT(YR/1000)
550 D2=INT((YR-1000*D1)/100)
560 D3=INT(Y/10)
570 D4=Y-10*D3
580 FORJ=4TO0STEP-1
590 IF((2^J)ANDN(D1,I))>0THENPRINT#1,"*";ELSEPRINT#1," ";
600 NEXT J
610 PRINT#1," ";
620 FORJ=4TO0STEP-1
630 IF((2^J)ANDN(D2,I))>0THENPRINT#1,"*";ELSEPRINT#1," ";
640 NEXT J
650 PRINT#1," ";
660 FORJ=4TO0STEP-1
670 IF((2^J)ANDN(D3,I))>0THENPRINT#1,"*";ELSEPRINT#1," ";
680 NEXT J
690 PRINT#1," ";
700 FORJ=4TO0STEP-1
710 IF((2^J)ANDN(D4,I))>0THENPRINT#1,"*";ELSEPRINT#1," ";
720 NEXT J
730 PRINT#1,
740 NEXT I
750 RETURN
760 '
770 'Output Calendar
780 PRINT#1,"   "STRING$(75,"*")
790 FORI=0TO3
800 PRINT#1,"   "STRING$(75,"*")
810 IF I=0THENPRINT#1,"          January         *          February        *           March"
820 IF I=1THENPRINT#1,"           April          *             May          *            June"
830 IF I=2THENPRINT#1,"           July           *           August         *         September"
840 IF I=3THENPRINT#1,"         October          *          November        *         December"
850 FORII=0TO2
860 PRINT#1,"    S  M  T  W  T  F  S";
870 IFII<2THENPRINT#1,"   *";
880 NEXTII
890 PRINT#1,
900 FORL=0TO5
910 FORK=0TO2
920 D2=0:FOR J=0TO6
930 IFD2=0THENPRINT#1,"  ";
940 DT=M(3*I+K,J+L*7)
950 IF DT=0THENPRINT#1,"   ";ELSEPRINT#1,USING"###";DT;
960 D2=D2+1
970 IF D2<7THEN990
980 D2=0:IFK<2THENPRINT#1,"   *";:ELSEPRINT#1,
990 NEXT J
1000 NEXT K
1010 NEXTL
1020 NEXT I
1030 PRINT#1,"   "STRING$(75,"*")
1040 PRINT#1,"   "STRING$(75,"*")
1050 RETURN
1060 '
1070 'Data Section
1080 DATA 14,17,17,17,17,17,14:'0
1090 DATA 1,3,5,1,1,1,1:'1
1100 DATA 14,17,2,4,8,16,31:'2
1110 DATA 30,1,1,14,1,1,30:'3
1120 DATA 18,18,18,31,2,2,2:'4
1130 DATA 31,16,16,30,1,1,30:'5
1140 DATA 14,17,16,30,17,17,14:'6
1150 DATA 31,1,2,4,8,16,16:'7
1160 DATA 14,17,17,14,17,17,14:'8
1170 DATA 14,17,17,15,1,17,14:'9

